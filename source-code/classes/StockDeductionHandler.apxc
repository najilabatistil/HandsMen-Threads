public class StockDeductionHandler {
    public static void deductStock(List<HandsMen_Order__c> orders) {
        Map<Id, Inventory__c> productToInventory = new Map<Id, Inventory__c>();
        Set<Id> productIds = new Set<Id>();

        for (HandsMen_Order__c order : orders) {
            if (order.Status__c == 'Confirmed' && order.HandsMen_Product__c != null) {
                productIds.add(order.HandsMen_Product__c);
            }
        }

        if (productIds.isEmpty()) return;

        for (Inventory__c inv : [
            SELECT Id, Stock_Quantity__c, Product__c
            FROM Inventory__c
            WHERE Product__c IN :productIds
        ]) {
            productToInventory.put(inv.Product__c, inv);
        }

        for (HandsMen_Order__c order : orders) {
            if (order.Status__c == 'Confirmed' && order.HandsMen_Product__c != null) {
                Inventory__c inv = productToInventory.get(order.HandsMen_Product__c);
                if (inv != null) {
                    inv.Stock_Quantity__c -= order.Quantity__c;
                }
            }
        }

        if (!productToInventory.isEmpty()) {
            update productToInventory.values();
        }
    }
}